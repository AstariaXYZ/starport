// SPDX-License-Identifier: BUSL-1.1
//
//                       ↑↑↑↑                 ↑↑
//                       ↑↑↑↑                ↑↑↑↑↑
//                       ↑↑↑↑              ↑   ↑
//                       ↑↑↑↑            ↑↑↑↑↑
//            ↑          ↑↑↑↑          ↑   ↑
//          ↑↑↑↑↑        ↑↑↑↑        ↑↑↑↑↑
//            ↑↑↑↑↑      ↑↑↑↑      ↑↑↑↑↑                                   ↑↑↑                                                                      ↑↑↑
//              ↑↑↑↑↑    ↑↑↑↑    ↑↑↑↑↑                          ↑↑↑        ↑↑↑         ↑↑↑            ↑↑         ↑↑            ↑↑↑            ↑↑    ↑↑↑
//                ↑↑↑↑↑  ↑↑↑↑  ↑↑↑↑↑                         ↑↑↑↑ ↑↑↑↑   ↑↑↑↑↑↑↑    ↑↑↑↑↑↑↑↑↑     ↑↑ ↑↑↑   ↑↑↑↑↑↑↑↑↑↑↑     ↑↑↑↑↑↑↑↑↑↑    ↑↑↑ ↑↑↑  ↑↑↑↑↑↑↑
//                  ↑↑↑↑↑↑↑↑↑↑↑↑↑↑                           ↑↑     ↑↑↑    ↑↑↑     ↑↑↑     ↑↑↑    ↑↑↑      ↑↑↑      ↑↑↑   ↑↑↑      ↑↑↑   ↑↑↑↑       ↑↑↑
//                    ↑↑↑↑↑↑↑↑↑↑                             ↑↑↑↑↑         ↑↑↑            ↑↑↑↑    ↑↑       ↑↑↑       ↑↑   ↑↑↑       ↑↑↑  ↑↑↑        ↑↑↑
//  ↑↑↑↑  ↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑   ↑↑↑   ↑↑↑             ↑↑↑↑↑↑↑    ↑↑↑     ↑↑↑↑↑↑  ↑↑↑    ↑↑       ↑↑↑       ↑↑↑  ↑↑↑       ↑↑↑  ↑↑↑        ↑↑↑
//  ↑↑↑↑  ↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑   ↑↑↑   ↑↑↑                  ↑↑    ↑↑↑     ↑↑      ↑↑↑    ↑↑       ↑↑↑      ↑↑↑   ↑↑↑      ↑↑↑   ↑↑↑        ↑↑↑
//                    ↑↑↑↑↑↑↑↑↑↑                             ↑↑↑    ↑↑↑    ↑↑↑     ↑↑↑    ↑↑↑↑    ↑↑       ↑↑↑↑↑  ↑↑↑↑     ↑↑↑↑   ↑↑↑    ↑↑↑        ↑↑↑
//                  ↑↑↑↑↑↑↑↑↑↑↑↑↑↑                             ↑↑↑↑↑↑       ↑↑↑↑     ↑↑↑↑↑ ↑↑↑    ↑↑       ↑↑↑ ↑↑↑↑↑↑        ↑↑↑↑↑↑      ↑↑↑          ↑↑↑
//                ↑↑↑↑↑  ↑↑↑↑  ↑↑↑↑↑                                                                       ↑↑↑
//              ↑↑↑↑↑    ↑↑↑↑    ↑↑↑↑                                                                      ↑↑↑     Starport: Lending Kernel
//                ↑      ↑↑↑↑     ↑↑↑↑↑
//                       ↑↑↑↑       ↑↑↑↑↑                                                                          Designed with love by Astaria Labs, Inc
//                       ↑↑↑↑         ↑
//                       ↑↑↑↑
//                       ↑↑↑↑
//                       ↑↑↑↑
//                       ↑↑↑↑

pragma solidity ^0.8.17;

import {PausableNonReentrant} from "starport-core/lib/PausableNonReentrant.sol";
import {Test} from "forge-std/Test.sol";
import {Ownable} from "solady/src/auth/Ownable.sol";

contract TestPausableNonReentrant is Test {
    PausableNonReentrantImpl impl = new PausableNonReentrantImpl();

    event Paused();
    event Unpaused();

    //test pause and unpause
    function testPauseAndUnpause() public {
        impl.test();

        vm.expectEmit(address(impl));
        emit Paused();
        impl.pause();
        assertTrue(impl.paused());

        vm.expectRevert(PausableNonReentrant.IsPaused.selector);
        impl.test();

        vm.expectEmit(address(impl));
        emit Unpaused();
        impl.unpause();
        assertFalse(impl.paused());

        impl.test();
    }

    //test not owner
    function testNotOwner() public {
        vm.prank(address(0x5));
        vm.expectRevert(Ownable.Unauthorized.selector);
        impl.pause();

        impl.pause();

        vm.expectRevert(Ownable.Unauthorized.selector);
        vm.prank(address(0x5));
        impl.unpause();
    }

    //test reentrancy
    function testReentrancy() public {
        impl.testReentrancy();
    }

    //test unpause when not paused, prevents owner from overriding lock
    function testUnpauseWhenNotPaused() public {
        vm.expectRevert(PausableNonReentrant.NotPaused.selector);
        impl.unpause();
    }

    //fallback for reentrancy test
    fallback() external {
        //test reentrancy guard
        vm.expectRevert(PausableNonReentrant.IsLocked.selector);
        impl.testReentrancy();

        //test pause during reentrancy, prevents owner from overriding lock
        vm.expectRevert(PausableNonReentrant.IsLocked.selector);
        impl.pause();
    }
}

contract PausableNonReentrantImpl is PausableNonReentrant {
    constructor() {
        _initializeOwner(msg.sender);
    }

    function test() public pausableNonReentrant {}

    function testReentrancy() public pausableNonReentrant {
        (bool success,) = msg.sender.call("");
        if (!success) {
            revert("Call failed");
        }
    }
}
